if (rootProject.ext.module_home.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


//可以在Gradle Console窗口打印出信息
//println rootProject.ext.module_home.toBoolean()

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion



    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //ARouter路由配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (rootProject.ext.module_home.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发环境下自动排序所以debug下的java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    //给不同的module设置不同的资源前缀，避免资源名重复
    resourcePrefix "module_home"

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':module_component_base')


    if (!rootProject.ext.module_common_ui.toBoolean()) {
        implementation project(':module_common_ui')
    }

    if (!rootProject.ext.module_app.toBoolean()) {
        implementation project(':module_app')
    }

    //路由
    annotationProcessor rootProject.ext.dependencies.arouter_compiler
    implementation rootProject.ext.dependencies.arouter_api

    implementation rootProject.ext.dependencies.recyclerview_v7
    implementation rootProject.ext.dependencies.gson
}
